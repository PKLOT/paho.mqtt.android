apply plugin: 'com.android.library'
apply plugin: 'maven-publish'

android {
    compileSdkVersion rootProject.ext.compileSdkVersion

    defaultConfig {
        minSdkVersion 9
        targetSdkVersion 31

        testApplicationId "org.eclipse.paho.android.service.test"
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    dexOptions {
        preDexLibraries = false
    }

    lintOptions {
        abortOnError false
    }
}

repositories {
    maven { url rootProject.ext.mavenUrl }
    mavenLocal()
}

configurations {
   //androidTestCompile.exclude module: 'org.eclipse.paho.client.mqttv3'
}

dependencies {
    api "org.eclipse.paho:org.eclipse.paho.client.mqttv3:${rootProject.ext.clientVersion}"
    api "com.android.support:support-v4:${rootProject.ext.supportLibVersion}"
    api fileTree(include: ['*.jar'], dir: 'libs')
}

task androidSourcesJar(type: Jar) {
    archiveClassifier.set('sources')
    from android.sourceSets.main.java.srcDirs
}

artifacts {
    archives androidSourcesJar
}

afterEvaluate {
    publishing {
        repositories {
            maven {
                name = "GitHubPackages"
                url = uri("https://maven.pkg.github.com/PKLOT/paho.mqtt.android")
                credentials {
                    username = System.getenv("CI_GITHUB_USER_NAME")
                    password = System.getenv("CI_GITHUB_PACKAGE_READ_WRITE_PERSONAL_ACCESS_TOKEN")
                }
            }
        }
        publications {
            gpr(MavenPublication) {
                from(components.release)
                artifact androidSourcesJar
                groupId 'com.pklotcorp'
                artifactId 'org.eclipse.paho.android.service'
                version '1.1.1'
            }
        }
    }
}
